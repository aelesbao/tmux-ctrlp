#!/usr/bin/env zsh

set -e

if ! (( ${+commands[fzf-tmux]} )); then
  echo "Required dependencies not found. Please install fzf" >&2
  exit 1
fi

local cache_dir="${XDG_CACHE_DIR:-$HOME/.cache}/tmux"

function list-panes {
  local index_format="#{session_last_attached}.#{window_active}#{pane_active}"
  local session_id_format="#{session_name}:#{window_index}.#{pane_index}"
  local -a panes_format=(
    "$(tput setaf 6) #{session_name}$(tput sgr0)"
    "$(tput setaf 5) #{window_name}$(tput sgr0)"
    "$(tput setaf 4) #{s@$HOME@~@:pane_current_path}$(tput sgr0)"
    "$(tput setaf 3) #{pane_current_command}$(tput sgr0)"
  )
  local format="${index_format}##${session_id_format}##${(j. | .)panes_format}"

  tmux list-panes -a -F "${format}" | sort -n -r
}

function fzf-sessions {
  [[ -d "$cache_dir" ]] || mkdir-p "$cache_dir"

  list-panes | fzf-tmux -p 80% --ansi \
    --scheme=history \
    --tiebreak=index \
    --history="$cache_dir/ctrlp-session-history" \
    --delimiter=\# --with-nth=3 \
    --preview='tmux capture-pane -t {2} -p -e -N' \
    --preview-window='70%:bottom' \
    --border-label='Choose the pane to focus'
}

function main {
  # target format = session:window.pane
  local target="$1"

  if [[ -z "$target" ]]; then
    target="$(fzf-sessions | cut -d '#' -f2)"
    [[ -n "$target" ]] || return 0
  fi

  # Switch or attach to the session
  if [ -z "$TMUX" ]; then
    tmux attach -t "$target"
  else
    tmux switch-client -t "$target"
  fi
}

main "$@"

# vim:ft=zsh
